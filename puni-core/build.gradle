/*
 * Copyright 2014 Jon Bonazza
 *
 * Jon Bonazza licenses this file to you under the Apache License, version
 * 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at:
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/groups/public"}
    }

    dependencies {
        classpath 'com.github.townsfolk:gradle-release:1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.0'
    id 'maven-publish'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

shadowJar {
    baseName='puni-core'
    classifier=''
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            groupId = 'com.github.jonbonazza'
            artifactId = 'puni-core'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'release'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.10.8'

    compile 'io.netty:netty-all:4.0.24.Final'
    compile 'com.google.guava:guava:18.0'
    compile 'org.yaml:snakeyaml:1.14'
}

bintray {
    user = "${BINTRAY_USER}"
    key = "${BINTRAY_API_KEY}"

    publications = ['shadow']
    publish = true
    pkg {
        repo = 'maven'
        name = 'puni-core'
        desc = 'Lightweight netty-based web container.'
        websiteUrl = 'https://github.com/jonbonazza/puni'
        issueTrackerUrl = ''
        vcsUrl = 'https://github.com/jonbonazza/puni.git'
        licenses = ['Apache-2.0']
        labels = []
        publicDownloadNumbers = true
    }
}

createReleaseTag.dependsOn("shadowJar")
createReleaseTag.dependsOn("sourceJar")
createReleaseTag.dependsOn("bintrayUpload")